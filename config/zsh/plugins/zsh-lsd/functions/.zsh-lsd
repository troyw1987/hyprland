# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2022 Salvydas Lukosius

builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
builtin setopt extended_glob warn_create_global typeset_silent local_options

typeset -i enable_autocd=0 ret=0

if [[ $TERM == 'dumb' ]]; then
  ret=0
elif (( ! $+commands[lsd] )); then
  print "Please install lsd before using this plugin." >&2
  ret=1
else
  typeset -ag lsd_params
  lsd_params=( "--config-file=${Plugins[ZSH_LSD]}/config.yaml" )

  alias ls='command lsd $lsd_params'
  alias ll='command lsd --all --header --long $lsd_params'
  alias llm='command lsd --all --header --long --sort time $lsd_params'
  alias la='command lsd -lhiUa --group-directories-first'
  alias lx='command lsd -lhiUSA --group-directories-first --total-size'
  alias lt='command lsd --tree $lsd_params'
  alias tree='command lsd --tree $lsd_params'

  [[ "$AUTOCD" = <-> ]] && enable_autocd="$AUTOCD"

  if [[ "$enable_autocd" == "1" ]]; then
    # Function for cd auto list directories
    →auto-lsd() { command lsd $lsd_params; }
    [[ $chpwd_functions[(r)→auto-lsd] == →auto-lsd ]] || chpwd_functions=( →auto-lsd $chpwd_functions )
  fi
  
  ret=$?
fi

return $ret
